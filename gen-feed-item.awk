# The MIT License (MIT)

# Copyright (c) 2016 nabijaczleweli

# Permission is hereby granted, free of charge, to any person obtaining a copy of
# this software and associated documentation files (the "Software"), to deal in
# the Software without restriction, including without limitation the rights to
# use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
# the Software, and to permit persons to whom the Software is furnished to do so,
# subject to the following conditions:

# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
# FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
# COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
# IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
# CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


function escape_html(text) {
	text = gensub("&", "\\&amp;", "g", text)
	text = gensub("<", "\\&lt;", "g", text)
	text = gensub(">", "\\&gt;", "g", text)
	text = gensub("\"", "\\&quot;", "g", text)
	return text
}

function handle_line(line, fname) {
	if(line ~ /^#include ".+"/) {
		inc_f = gensub(/\/\//, "/", "g", gensub(/^#include "([^"]+)"/, gensub(/(.+)\/.*/, "\\1", "g", fname) "/\\1", "g", line))
		while(("cat " inc_f | getline line) > 0) {
			handle_line(line, inc_f)
		}
		close("cat " inc_f)
	} else {
		everything = everything line "\n"
	}
}


BEGIN {
	title = ""
	description = ""
	author = ""
	pubDate = ""
	everything = ""
}

/ \(c\) by / {
	author = gensub(/.* \(c\) by (.*)/, "\\1", "g")
}

/^BOILERPLATE\(.*\)/ {
	title = gensub(/BOILERPLATE\(([^,]+), ([^,]+).*\)/, "\\1", "g") " â€” " author
	description = gensub(/BOILERPLATE\(([^,]+), ([^,]+).*\)/, "\\2", "g")
}

/#define RSS_PUB_DATE/ {
	pubDate = gensub(/#define RSS_PUB_DATE (.*)/, "\\1", "g")
}

/^BOILERPLATE/,/^BOILERPLATE_END/ {
	handle_line($0, "src/" filename)
}

END {
	# pubDate empty, which means that a page is not meant for RSS consumption
	if(pubDate == "")
		exit

	everything = gensub(/^BOILERPLATE\(.*\)\n\n\n/, "", "g", everything)
	everything = gensub(/\n\n[ABCDEFGHIJKLMNOPQRSTUVWXYZ_]*_END.*/, "", "g", everything)

	print ""
	print "    <item>"
	print "      <title>" title "</title>"
	print "      <description>" escape_html(everything) "</description>"
	print "      <author>" author "</author>"
	print "      <pubDate>" pubDate "</pubDate>"
	print "      <guid>https://nabijaczleweli.xyz/content/" filename "</guid>"
	print "      <source url=\"https://nabijaczleweli.xyz/content/feed.xml\">nabijaczleweli's page</source>"
	print "    </item>"
}
